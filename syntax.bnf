<query> ::= <key> '=' <value> | <key>

<key> ::= <path> | <tuple> | <path> <tuple>

<value> ::= <tuple> | <data> | 'clear'

<path> ::= '/' <directory> <path> | '/' <directory>

<tuple> ::= '(' <elements> ')'

<elements> ::= <data> ',' <elements> | <tuple> ',' <elements> | <data> | <tuple>

<data> ::= 'nil' | <bool> | <int> | <float> | <scientific> | <string> | <uuid> | <base64>

<bool> ::= 'true' | 'false'

; Whitespace after the '-' is not allowed.
<int> ::= <number> | '-'<number>

; Whitespace around the '.' is not allowed.
<float> ::= <int>'.'<number>

; Whitespace around the 'e' is not allowed.
<scientific> ::= <int>'e'<int> | <float>'e'<int>

<string> ::= '"'<words>'"'

<number> ::= <digit> | <digit><number>

<words> ::= <character><words> | <character>

; <digit> includes all single digit numbers 0-9.
; <character> includes all ASCII characters.