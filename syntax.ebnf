(*
 This document defines the syntax of the FDBQ query
 language. The meta language used is extended Backus-Naur
 form as defined in ISO/IEC 14977.
*)

query = keyval | key | directory ;

keyval = key, ws, '=', ws, value ;

key = directory, ws, tuple ;

value = tuple | data | 'clear' ;

directory = { '/', ws, words, ws } ;

tuple = '{', ws, elements, ws, '}' ;

elements = ( data | tuple ), ws, ',', ws, elements | data | tuple ;

data = 'nil' | bool | int | float | scientific | string | uuid | base64 ;

bool = 'true' | 'false' ;

int = number | '-', number ;

float = int, '.', number ;

scientific = int, 'e', int | float, 'e', int ;

string = '"', words, '"' ;

uuid = 8 * hex, '-', 4 * hex, '-', 4 * hex, '-', 4 * hex, '-', 12 * hex ;

number = { digit } ;

words = { character } ;

ws = { ' ' | '\t' | '\n' | '\r' } ;

digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

hex = digit | 'A' | 'B' | 'C' | 'D' | 'E' | 'F'

character = (* all ASCII characters *)
