(*
 This document defines the syntax of the FDBQ query
 language. The meta language used is extended Backus-Naur
 form as defined in ISO/IEC 14977.
*)

query = ws, ( keyval | key | directory ), ws ;

keyval = key, ws, '=', ws, value ;

key = directory, ws, tuple ;

value = data | 'clear' ;

directory = '/', ws, text, [ ws, directory ] ;

data = 'nil' | bool | int | float | scientific | string | uuid | hex | tuple ;

tuple = '{', [ ws, elements ], [ ws, ',', ws, '...' ], ws, '}' ;

elements = data, [ ws, ',', ws, elements ] ;

bool = 'true' | 'false' ;

int = [ '-' ], number ;

float = int, '.', number ;

scientific = ( int | float ), 'e', int ;

string = '"', text, '"' ;

uuid = 8 * hexDigit, '-', 4 * hexDigit, '-', 4 * hexDigit, '-', 4 * hexDigit, '-', 12 * hexDigit ;

hex = '0x', { 2 * hexDigit } ;

number = { digit } ;

text = { character } ;

digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;

hexDigit = digit | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' ;

character = ? Any single ASCII character from the "printable" character group (32-126). ? ;

ws = ? Any number of ASCII characters with codes 9 (Horizontal Tab), 10 (Line Feed), 13 (Carriage Return), or 32 (Space). ? ;
