(*
 This document defines the syntax of the FDBQ query
 language. The meta language used is extended Backus-Naur
 form as defined in ISO/IEC 14977.
*)


query = keyval | key | directory ;


keyval = key, '=', ws, value ;

key = directory, tuple ;

value = data | 'clear' ;


directory = '/', ( text | '<>' ), [ directory ] ;

tuple = '{', [ nl, elements, [ ',' ], nl ] '}' ;

elements = '...' | ( ( data | 'nil' ), [ ',', nl, elements ] ) ;


data = variable | tuple | bool | int | float | scientific | string | uuid | bytes ;

variable = '<', [ type ], '>' ;

type = ( 'tuple' | 'bool' | 'int' | 'float' | 'string' | 'uuid' | 'bytes' ), [ '|', type ] ;

bool = 'true' | 'false' ;

int = [ '-' ], number ;

float = int, '.', number ;

scientific = ( int | float ), 'e', int ;

string = '"', text, '"' ;

uuid = 8 * hexDigit, '-', 4 * hexDigit, '-', 4 * hexDigit, '-', 4 * hexDigit, '-', 12 * hexDigit ;

bytes = '0x', { 2 * hexDigit } ;


number = { digit } ;

text = { character } ;

digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;

hexDigit = digit | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' ;

character = ? Any single ASCII character from the "printable" character group (32-126). ? ;

ws = ? Any number of ASCII characters with codes 9 (Horizontal Tab) or 32 (Space). ? ;

nl = ? Any number of ASCII characters with codes 9 (Horizontal Tab) 10 (Line Feed) 13 (Carriage Return) or 32 (Space). ? ;
