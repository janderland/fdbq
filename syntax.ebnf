(*
 This document defines the syntax of the FDBQ query language. The meta language
 used is extended Backus-Naur form as defined in ISO/IEC 14977.
*)

query = keyval | key | directory ;

keyval = key, ws, '=', ws, value ;

key = directory, tuple ;

value = data | 'clear' ;

directory = { '/', ws, words, ws } ;

tuple = '{', ws, elements, ws, '}' ;

elements = data, ws, ',', ws, elements | data ;

data = 'nil' | int | bool | float | scientific | string | bytes | uuid | tuple | variable;

variable = '<', ws, types, ws, '>' ;

types = primitive, ws, '|', ws, types | primitive ;

primitive = 'int' | 'uint' | 'bool' | 'float' | 'bigint' | 'string' | 'bytes' | 'uuid' | 'tuple' ;

bool = 'true' | 'false' ;

int = number | '-', number ;

float = int, '.', number ;

scientific = int, 'e', int | float, 'e', int ;

string = '"', words, '"' ;

uuid = 8 * hex, '-', 4 * hex, '-', 4 * hex, '-', 4 * hex, '-', 12 * hex ;

bytes = '0x', { 2 * hex } ;

number = { digit } ;

words = { character } ;

digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

hex = digit | 'A' | 'B' | 'C' | 'D' | 'E' | 'F'

character = ? Any single ASCII character from the "printable" character group (32-126). ?

ws = ? Any number of ASCII characters with codes 9 (Horizontal Tab), 10 (Line Feed), 13 (Carriage Return), or 32 (Space). ? ;

